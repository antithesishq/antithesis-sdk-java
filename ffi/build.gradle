plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
  // none
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            pom {
                name = 'Antithesis FFI for Java'
                description = 'This internal library provides methods for Java programs to interface with Antithesis platform.'
                url = 'https://antithesis.com/docs/using_antithesis/sdk/java/overview.html'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/antithesishq/antithesis-sdk-java/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'Antithesis'
                        name = 'Antithesis HQ'
                        email = 'noreply@antithesis.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/antithesishq/antithesis-sdk-java.git'
                    developerConnection = 'scm:git:ssh://github.com/antithesishq/antithesis-sdk-java.git'
                    url = 'https://github.com/antithesishq/antithesis-sdk-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += ["-Xlint:-options"]
}

task buildFfiBridge(type: Exec) {
    workingDir projectDir
    if (file("./ffi_build.sh").isFile()) {
        commandLine 'sh', '-c', './ffi_build.sh'
    }
    else {
        // Enter the swig directory and make the 'build' target
        commandLine 'make', '-C', 'swig', 'build'
    }
}
compileJava.dependsOn buildFfiBridge
compileTestJava.dependsOn buildFfiBridge

task cleanFfiBridge(type: Exec) {
    workingDir projectDir
    if (!file("./ffi_build.sh").isFile()) {
        // Enter the swig directory and make the 'clean' target
        commandLine 'make', '-C', 'swig', 'clean'
    }
}
clean.dependsOn cleanFfiBridge

jar {
    archiveClassifier = ''
    archiveAppendix = 'ffi'
    archiveBaseName = 'antithesis'
    manifest {
        attributes 'Implementation-Title': 'Antithesis FFI for Java', // !! LOAD BEARING Title
                'Implementation-Version': "${project.version}",
                'Specification-Title': 'Antithesis SDK Protocol',
                'Specification-Version': "${project.property('com.antithesis.sdk.protocol')}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    }
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}
